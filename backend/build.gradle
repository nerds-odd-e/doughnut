buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'cz.habarta.typescript-generator:typescript-generator-gradle-plugin:2.36.1070'
  }
}

plugins {
  id 'org.springframework.boot' version '2.7.3'
  id 'io.spring.dependency-management' version '1.0.13.RELEASE'
  id 'java'
  // Spotless for the code style
  id "com.diffplug.spotless" version "6.10.0"
}

apply plugin: 'cz.habarta.typescript-generator'

group = 'com.odde'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 17
targetCompatibility = 17

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.flywaydb:flyway-mysql:9.2.2'
  compileOnly	 'org.flywaydb:flyway-core:9.2.2'
  implementation 'org.springframework.session:spring-session-core'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
  compileOnly	 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  runtimeOnly	 'mysql:mysql-connector-java'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation  'org.springframework.boot:spring-boot-starter-test'
}

task testDBMigrate(type: Test) {
  useJUnitPlatform {
    includeTags 'dbMigrate'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
}

tasks.register("bootRunDev") {
  group = "application"
  description = "Runs Doughnut SpringBoot application in dev profile"
  doFirst {
    tasks.bootRun.configure {
      systemProperty("spring.profiles.active", "dev")
      systemProperty("spring.datasource.url", "jdbc:mysql://127.0.0.1:3309/doughnut_development")
      systemProperty("spring.datasource.username", "doughnut")
      systemProperty("spring.datasource.username", "doughnut")
    }
  }
  finalizedBy("bootRun")
}

tasks.register("bootRunTest") {
  group = "application"
  description = "Runs Doughnut SpringBoot application in test profile"
  doFirst {
    tasks.bootRun.configure {
      systemProperty("spring.profiles.active", "test")
      systemProperty("spring.datasource.url", "jdbc:mysql://127.0.0.1:3309/doughnut_test")
      systemProperty("spring.datasource.username", "doughnut")
      systemProperty("spring.datasource.username", "doughnut")
    }
  }
  finalizedBy("bootRun")
}

tasks.register("bootRunE2E") {
  group = "application"
  description = "Runs Doughnut SpringBoot application in e2e profile"
  doFirst {
    tasks.bootRun.configure {
      systemProperty("spring.profiles.active", "e2e")
    }
  }
  finalizedBy("bootRun")
}

spotless {
  java {
    importOrder() // standard import order
    removeUnusedImports()
    googleJavaFormat()
    targetExclude("build/**")
  }
}

tasks.register("lint") {
  dependsOn spotlessCheck
}

generateTypeScript {
  noFileComment = true
  jsonLibrary = 'jackson2'
  classPatterns = [
    'com.odde.doughnut.entities.json.*',
  ]
  optionalAnnotations = ['org.springframework.lang.Nullable']
  customTypeMappings = [
    'java.sql.Timestamp:string'
  ]
  outputFile = '../frontend/src/@types/generated/rest.d.ts'
  outputKind = 'global'
  namespace = 'Generated';
}
